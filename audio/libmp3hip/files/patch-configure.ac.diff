--- configure.in	2002-04-05 11:50:56.000000000 -0500
+++ configure.ac	2013-08-12 21:19:07.000000000 -0400
@@ -1,62 +1,80 @@
-dnl Process this file with autoconf to produce a configure script
+dnl#                                               -*- Autoconf -*-
+dnl# Process this file with autoconf to produce a configure script
 
-dnl ------------------------------------------------
-dnl Initialization and Versioning
-dnl ------------------------------------------------
-
-AC_INIT(lib/hip.c)
-AM_INIT_AUTOMAKE(libmp3hip,0.1)
-AC_REVISION($Id: configure.in,v 1.3 2002/04/05 16:50:56 myers_carpenter Exp $)
-
-dnl Library versioning
-
-dnl CURRENT
-dnl      The most recent interface number that this library implements.
-dnl 
-dnl REVISION
-dnl      The implementation number of the CURRENT interface.
-dnl 
-dnl AGE
-dnl      The difference between the newest and oldest interfaces that this
-dnl      library implements.  In other words, the library implements all the
-dnl      interface numbers in the range from number 'CURRENT - AGE' to
-dnl      'CURRENT'.
+dnl#------------------------------------------------
+dnl# Initialization and Versioning
+dnl#------------------------------------------------
+
+AC_PREREQ([2.69])
+AC_INIT([libmp3hip], [0.1.2.1])
+AC_CONFIG_SRCDIR([examples/hip_example.c])
+AC_CONFIG_HEADERS([config.h])
+AC_CONFIG_MACRO_DIR([m4])
+AM_INIT_AUTOMAKE([foreign -Wall])
+AM_MAINTAINER_MODE
+AC_REVISION([$Id: configure.in,v 1.3 2002/04/05 16:50:56 myers_carpenter Exp $])
+
+dnl# Library versioning
+
+dnl# CURRENT
+dnl#      The most recent interface number that this library implements.
+dnl# 
+dnl# REVISION
+dnl#      The implementation number of the CURRENT interface.
+dnl# 
+dnl# AGE
+dnl#      The difference between the newest and oldest interfaces that this
+dnl#      library implements.  In other words, the library implements all the
+dnl#      interface numbers in the range from number 'CURRENT - AGE' to
+dnl#      'CURRENT'.
 
 H_LIB_CURRENT=0
 H_LIB_REVISION=0
 H_LIB_AGE=0
-AC_SUBST(H_LIB_CURRENT)
-AC_SUBST(H_LIB_REVISION)
-AC_SUBST(H_LIB_AGE)
-
-dnl --------------------------------------------------  
-dnl Check for programs
-dnl --------------------------------------------------  
+AC_SUBST([H_LIB_CURRENT])
+AC_SUBST([H_LIB_REVISION])
+AC_SUBST([H_LIB_AGE])
+
+dnl#--------------------------------------------------  
+dnl# Check for programs
+dnl#--------------------------------------------------  
 
-dnl save $CFLAGS since AC_PROG_CC likes to insert "-g -O2"
-dnl if $CFLAGS is blank
+dnl# save $CFLAGS since AC_PROG_CC likes to insert "-g -O2"
+dnl# if $CFLAGS is blank
 cflags_save="$CFLAGS"
 AC_PROG_CC
 AC_PROG_CPP
+AC_PROG_CXX
+AC_PROG_GCC_TRADITIONAL
 CFLAGS="$cflags_save"
 
-AM_PROG_LIBTOOL
+AC_PROG_EGREP
+AC_PROG_INSTALL
+AC_PROG_LN_S
+LT_INIT
+if test "x$RANLIB" = "x"; then
+	AC_PROG_RANLIB
+else
+	export RANLIB
+fi
 
-dnl --------------------------------------------------
-dnl Set build flags based on environment
-dnl --------------------------------------------------
+dnl#--------------------------------------------------
+dnl# Set build flags based on environment
+dnl#--------------------------------------------------
 
 AC_CANONICAL_HOST
+AC_OBJEXT
+AC_EXEEXT
 
-dnl Set some target options
+dnl# Set some target options
 
 cflags_save="$CFLAGS"
 ldflags_save="$LDFLAGS"
 if test -z "$GCC"; then
 	case $host in 
 	*-*-irix*)
-		dnl If we're on IRIX, we wanna use cc even if gcc 
-		dnl is there (unless the user has overriden us)...
+		dnl# If we're on IRIX, we wanna use cc even if gcc 
+		dnl# is there (unless the user has overriden us)...
 		if test -z "$CC"; then
 			CC=cc
 		fi
@@ -78,7 +96,6 @@
 	*86-*-linux*)
 		DEBUG="-g -Wall -W -D_REENTRANT -D__NO_MATH_INLINES -fsigned-char"
 		CFLAGS="-O20 -ffast-math -mno-ieee-fp -D_REENTRANT -fsigned-char"
-#              	PROFILE="-Wall -W -pg -g -O20 -ffast-math -D_REENTRANT -fsigned-char -fno-inline -static"
 		PROFILE="-Wall -W -pg -g -O20 -ffast-math -mno-ieee-fp -D_REENTRANT -fsigned-char -fno-inline"
 
 		# glibc < 2.1.3 has a serious FP bug in the math inline header
@@ -86,18 +103,18 @@
 		# clobber is missing in the mathinline header, thus indicating
 		# the buggy version
 
-		AC_EGREP_CPP(log10.*fldlg2.*fxch,[
+		AC_EGREP_CPP([log10.*fldlg2.*fxch],[
 				#define __LIBC_INTERNAL_MATH_INLINES 1
 			     	#define __OPTIMIZE__
 				#include <math.h>
-				],bad=maybe,bad=no)
+				],[bad=maybe],[bad=no])
 		if test ${bad} = "maybe" ;then
-		      AC_EGREP_CPP(log10.*fldlg2.*fxch.*st\([[0123456789]]*\),
+		      AC_EGREP_CPP([log10.*fldlg2.*fxch.*st\([[0123456789]]*\)],
 					[
 					#define __LIBC_INTERNAL_MATH_INLINES 1
 				     	#define __OPTIMIZE__
 					#include <math.h>
-					],bad=no,bad=yes)
+					],[bad=no],[bad=yes])
 		fi
  		if test ${bad} = "yes" ;then
  AC_MSG_WARN([                                                        ])
@@ -125,8 +142,8 @@
 		PROFILE="-pg -g -O20 -D__NO_MATH_INLINES -fsigned-char -mv8" ;;
 	*-*-darwin*)
 		DEBUG="-DDARWIN -fno-common -force_cpusubtype_ALL -Wall -g -O0 -fsigned-char"
-		CFLAGS="-DDARWIN -fno-common -force_cpusubtype_ALL -Wall -g -O4 -ffast-math -fsigned-char"
-		PROFILE="-DDARWIN -fno-common -force_cpusubtype_ALL -Wall -g -pg -O4 -ffast-math -fsigned-char";;
+		CFLAGS="-DDARWIN -fno-common -force_cpusubtype_ALL -Wall -g -Os -fsigned-char"
+		PROFILE="-DDARWIN -fno-common -force_cpusubtype_ALL -Wall -g -pg -O3 -ffast-math -fsigned-char";;
 	*)
 		DEBUG="-g -Wall -W -D__NO_MATH_INLINES -fsigned-char"
 		CFLAGS="-O20 -D__NO_MATH_INLINES -fsigned-char"
@@ -136,62 +153,79 @@
 CFLAGS="$CFLAGS $cflags_save"
 LDFLAGS="$LDFLAGS $ldflags_save"
 
-dnl --------------------------------------------------
-dnl Check if we should build the python module
-dnl --------------------------------------------------
-AC_MSG_CHECKING(whether to build python module)
-AC_ARG_ENABLE(python,
-  [  --enable-python         Build python module.],
-  CONFIG_PYTHONMOD="${enableval}", CONFIG_PYTHONMOD="no")
+dnl#--------------------------------------------------
+dnl# Check if we should build the python module
+dnl#--------------------------------------------------
+AC_MSG_CHECKING([whether to build python module])
+AC_ARG_ENABLE([python],
+  [AS_HELP_STRING([--enable-python],[Build python module.])],
+  [CONFIG_PYTHONMOD="${enableval}"], [CONFIG_PYTHONMOD="no"])
 
 case "${CONFIG_PYTHONMOD}" in
 yes)
 	BUILD_PYTHON_MOD="yes"
+	AM_PATH_PYTHON([2.3])
+	AC_SUBST([PYTHON])
 	;;
 no)
 	BUILD_PYTHON_MOD="no"
 	;;
 *)
-	AC_MSG_ERROR(bad value »${CONFIG_PYTHONMOD}« for python option)
+	AC_MSG_ERROR([bad value »${CONFIG_PYTHONMOD}« for python option])
 	;;
 esac
-AC_SUBST(BUILD_PYTHON_MOD)
-AC_MSG_RESULT(${CONFIG_PYTHONMOD})
-
-dnl --------------------------------------------------
-dnl Check for headers
-dnl --------------------------------------------------
-
-AC_CHECK_HEADER(memory.h,CFLAGS="$CFLAGS -DUSE_MEMORY_H",:)
-
-dnl --------------------------------------------------
-dnl Check for typedefs, structures, etc
-dnl --------------------------------------------------
+AC_SUBST([BUILD_PYTHON_MOD])
+AC_MSG_RESULT([${CONFIG_PYTHONMOD}])
 
-dnl none
-
-dnl --------------------------------------------------
-dnl Check for libraries
-dnl --------------------------------------------------
-
-AC_CHECK_LIB(m, cos, LIBS="-lm", LIBS="")
-
-dnl --------------------------------------------------
-dnl Check for library functions
-dnl --------------------------------------------------
+dnl#--------------------------------------------------
+dnl# Check for headers
+dnl#--------------------------------------------------
+
+AC_CHECK_HEADERS([assert.h common.h endian.h fcntl.h math.h mpg123.h stdio.h secure/_string.h])
+AC_CHECK_HEADER([memory.h],[CFLAGS="$CFLAGS -DUSE_MEMORY_H"],[:])
+
+dnl#--------------------------------------------------
+dnl# Check for typedefs, structures, etc
+dnl#--------------------------------------------------
+
+AC_C_BIGENDIAN
+AC_TYPE_INT64_T
+AC_TYPE_UINT64_T
+AC_TYPE_SIZE_T
+AC_CHECK_DECLS([__BIG_ENDIAN])
+AC_CHECK_DECLS([__BYTE_ORDER])
+
+dnl#--------------------------------------------------
+dnl# Check for libraries
+dnl#--------------------------------------------------
+
+AC_CHECK_LIB([m],[cos],[LIBS="-lm"],[LIBS=""])
+
+dnl#--------------------------------------------------
+dnl# Check for library functions
+dnl#--------------------------------------------------
 
 AC_FUNC_ALLOCA
+AC_FUNC_MALLOC
 AC_FUNC_MEMCMP
+AC_CHECK_FUNCS([bcopy memcpy memmove memset pow sqrt strchr strrchr])
 
-dnl --------------------------------------------------
-dnl Do substitutions
-dnl --------------------------------------------------
+dnl#--------------------------------------------------
+dnl# Do substitutions
+dnl#--------------------------------------------------
 
 LIBS="$LIBS"
 
-AC_SUBST(LIBS)
-AC_SUBST(DEBUG)
-AC_SUBST(PROFILE)
-AC_SUBST(pthread_lib)
-
-AC_OUTPUT(Makefile lib/Makefile doc/Makefile include/Makefile examples/Makefile debian/Makefile python/Makefile)
+AC_SUBST([LIBS])
+AC_SUBST([DEBUG])
+AC_SUBST([PROFILE])
+AC_SUBST([pthread_lib])
+
+AC_CONFIG_FILES([Makefile \
+                 lib/Makefile \
+                 doc/Makefile \
+                 include/Makefile \
+                 examples/Makefile \
+                 debian/Makefile \
+                 python/Makefile])
+AC_OUTPUT
